#!/usr/bin/env perl

use v5.10.1;
use strict;
use warnings;

sub update_changelog ($$);
sub update_rules ($$);
sub gen_all_vers_cfg ();

my $version = shift or die "No version number specified.\n";
my $multi_upstream_version = shift or die "No version number specified.\n";

if ($version !~ /^\d+(\.\d+){3}$/) {
    die "ERROR: Bad version number: $version";
}

{
    my $file = "dubbo-nginx-module-plus-NGINX_VERSION.mk";
    open my $in, $file
        or die "Cannot open $file for reading: $!\n";
    my $new;
    my $changed;
    while (<$in>) {
        if (s/^ MOD_DUBBO_VER\ :=\ ( \d+(\.\d+){3} ) $/MOD_DUBBO_VER := $version/msx) {
            my $old_ver = $1;
            if ($old_ver ne $version) {
                $changed = 1;
                say "INFO: $file: version needs update from $old_ver to $version";

            } else {
                say "INFO: $file is already up to date: $old_ver";
                last;
            }
        }

        if (s/^ NGX_MULTI_UPSTREAM_MODULE_VER \ :=\ ( \d+(\.\d+){3} ) $/NGX_MULTI_UPSTREAM_MODULE_VER := $multi_upstream_version/msx) {
            my $old_ver = $1;
            if ($old_ver ne $multi_upstream_version) {
                say "INFO: $file: version needs update from $old_ver to $multi_upstream_version";

            } else {
                say "INFO: $file is already up to date: $old_ver";
            }
        }
    } continue {
        $new .= $_;
    }
    close $in;

    if ($changed) {
        open my $out, ">$file"
            or die "Cannot open $file for writing: $!\n";
        print $out $new;
        close $out;
        say "$file updated.";
    }
}

for my $dir (reverse sort glob 'dubbo-nginx-module-plus-NGINX_VERSION*') {
    next unless -d $dir && -d "$dir/debian";
    if ($dir =~ /^dubbo-nginx-module-plus-NGINX_VERSION$/) {
        my $name = $dir;
        my $changelog = "$dir/debian/changelog.tt2";
        if (!-f $changelog) {
            warn "Cannot find $changelog file\n";
            next;
        }

        #say $dir;
        update_changelog $name, $changelog;

        my $rules = "$dir/debian/rules";
        if (!-f $rules) {
            warn "Cannot find $rules file\n";
            next;
        }

        update_rules $name, $rules;
    }
}

gen_all_vers_cfg;

sub update_changelog ($$) {
    my ($name, $file) = @_;

    warn "name: $name, file: $file";

    open my $in, $file
        or die "Cannot open $file for reading: $!\n";
    my $src = do { local $/; <$in>; };
    close $in;

    if ($src !~ m/^ \Q$name\E \s+ \( \d+ /xsm) {
        die "Bad file content in $file";
    }

    if ($src =~ /^ \Q$name\E \s+ \( ( \d+(\.\d+){3} ) - \d+ ~ /xsm) {
        my $old_ver = $1;
        if ($old_ver ne $version) {
            say "INFO: $file: version needs update from $old_ver to $version";

        } else {
            say "INFO: $file is already up to date: $old_ver";
            return;
        }

    } else {
        die "$file: version not found";
    }

    my $now = `date +'%a, %e %b %Y %H:%M:%S %z'`;
    chomp $now;
    $now =~ s/\s\s+/ /g;
    #warn "today: $today";

    my $new = <<_EOC_;
$name ($version-1~[% distro %]1) [% distro %]; urgency=high

  * upgraded dubbo-nginx-module to $version.

 -- OpenResty Admin <admin\@openresty.com>  $now

_EOC_

    $src = $new . $src;

    open my $out, ">$file"
        or die "Cannot open $file for writing: $!\n";
    print $out $src;
    close $out;
    say "$file updated.";
}

sub update_rules ($$) {
    my ($name, $file) = @_;

    warn "name: $name, file: $file";

    open my $in, $file
        or die "Cannot open $file for reading: $!\n";
    my $src = do { local $/; <$in>; };
    close $in;

    #ngx_multi_upstream_module-1.2.0.2
    if ($src =~ s/ngx_multi_upstream_module-(\d+(?:\.\d+){3})/ngx_multi_upstream_module-$multi_upstream_version/xgsm) {
        say "change ngx-multi-upstream-module from $1 to $multi_upstream_version";
    }

    open my $out, ">$file"
        or die "Cannot open $file for writing: $!\n";
    print $out $src;
    close $out;
    say "$file updated.";
}

sub gen_all_vers_cfg () {
    my @ngx_vers = (["1.25.3", "1.25.3.2"]);

    for my $ver (@ngx_vers) {
        my $ngx_ver=$ver->[0];
        my $or_ver=$ver->[1];

        system("rm -fr dubbo-nginx-module-plus-$ngx_ver") == 0
            or die "Cannot remove dubbo-nginx-module-plus-$ngx_ver: $!\n";

        system("cp -R dubbo-nginx-module-plus-NGINX_VERSION dubbo-nginx-module-plus-$ngx_ver") == 0
            or die "Cannot cp to dubbo-nginx-module-plus-$ngx_ver: $!\n";

        rename "dubbo-nginx-module-plus-$ngx_ver/debian/dubbo-nginx-module-plus-NGINX_VERSION.install",
               "dubbo-nginx-module-plus-$ngx_ver/debian/dubbo-nginx-module-plus-$ngx_ver.install"
               or die "Cannot rename dubbo-nginx-module-plus-NGINX_VERSION.install: $!";
        system("find dubbo-nginx-module-plus-$ngx_ver/debian -type f | xargs sed -i s/NGINX_VERSION/$ngx_ver/g") == 0
            or die "can not change NGINX_VERSION: $!";

        system("find dubbo-nginx-module-plus-$ngx_ver/debian -type f | xargs sed -i s/OPENRESTY_VERSION/$or_ver/g") == 0
            or die "can not change OPENRESTY_VERSION: $!";

        system("cp -R dubbo-nginx-module-plus-NGINX_VERSION.mk dubbo-nginx-module-plus-$ngx_ver.mk") == 0
            or die "can not copy dubbo-nginx-module-plus-NGINX_VERSION.mk: $!";

        system("sed -i s/NGINX_VERSION/$ngx_ver/g dubbo-nginx-module-plus-$ngx_ver.mk") == 0
            or die "can not change NGINX_VERSION: $!";

        system("sed -i s/OPENRESTY_VERSION/$or_ver/g dubbo-nginx-module-plus-$ngx_ver.mk") == 0
            or die "can not change OPENRESTY_VERSION: $!";
    }
}
