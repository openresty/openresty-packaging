#!/usr/bin/env perl

use v5.10.1;
use strict;
use warnings;

sub update_spec ($);

my $version = shift or die "No version number specified.\n";

if ($version !~ /^\d+(?:\.\d+){2}$/) {
    die "ERROR: Bad version number: $version";
}

for my $specfile (reverse sort glob 'lua-kafka-nginx-module*.spec') {
    update_spec $specfile;
}

sub update_spec ($) {
    my $file = shift;
    open my $in, $file
        or die "Cannot open $file for reading: $!\n";
    my $src = do { local $/; <$in>; };
    close $in;

    if ($src !~ /^Name: \s+ lua-kafka-nginx-module\b/xsm) {
        die "Bad file content in $file";
    }

    my $changes = 0;

    if ($src =~ s/^Version: (\s+) ( \d+ (?: \.\d+ ){2} ) $/Version:$1$version/xsm) {
        my $old_ver = $2;
        if ($old_ver ne $version) {
            say "INFO: version got changed from $old_ver to $version";
            $changes++;

        } else {
            say "INFO: $file is already up to date: $version";
            return;
        }

    } else {
        die "$file: Version line not found";
    }

    if ($src =~ s/^Release: (\s+) (\d+) ( \%\{\?dist\} )  $/Release:${1}1$3/xsm) {
        my $old_rel = $2;
        if ($old_rel ne '1') {
            say "INFO: release number got changed from $old_rel to 1";
            $changes++;
        }

    } else {
        die "$file: Release line not found";
    }

    my $today = `date +'%a %b %e %Y'`;
    chomp $today;
    $today =~ s/\s\s+/ /g;
    #warn "today: $today";

    if ($src =~ s/^ (\%changelog\n) /${1}\* $today Yichun Zhang (agentzh) ${version}-1\n- upgraded lua-kafka-nginx-module to $version.\n/xsm) {
        $changes++;

    } else {
        die "$file: \%changelog section not found";
    }

    if ($changes > 0) {
        open my $out, ">$file"
            or die "Cannot open $file for writing: $!\n";
        print $out $src;
        close $out;
        say "$file updated with $changes change(s).";
        system("sed 's/NGINX_VERSION/1.21.4/' lua-kafka-nginx-module.spec | sed 's/OPENRESTY_VERSION/1.21.4.2rc1/' > lua-kafka-nginx-module-1.21.4.spec") == 0
                or die "Cannot generate spec for 1.21.4: $!";

        system("sed 's/NGINX_VERSION/1.25.3/' lua-kafka-nginx-module.spec | sed 's/OPENRESTY_VERSION/1.25.3.1/' > lua-kafka-nginx-module-1.25.3.spec") == 0
                or die "Cannot generate spec for 1.25.3: $!";
    }
}


