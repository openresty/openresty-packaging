#!/usr/bin/env perl

use v5.10.1;
use strict;
use warnings;

sub update_spec ($);

my $version = shift or die "No version number specified.\n";
my $multi_upstream_version = shift or die "No multi upstream version specified.";

if ($version !~ /^\d+(?:\.\d+){3}$/) {
    die "ERROR: Bad version number: $version";
}

if ($multi_upstream_version !~ /^\d+(?:\.\d+){3}$/) {
    die "ERROR: Bad version number: $version";
}

for my $specfile (reverse sort glob 'dubbo-nginx-module-plus.spec') {
    update_spec $specfile;
}

sub update_spec ($) {
    my $file = shift;
    open my $in, $file
        or die "Cannot open $file for reading: $!\n";
    my $src = do { local $/; <$in>; };
    close $in;

    if ($src !~ /^Name: \s+ dubbo-nginx-module-plus\b/xsm) {
        die "Bad file content in $file";
    }

    my $changes = 0;
    my $dubbo_change = 0;
    my $multi_up_change = 0;

    if ($src =~ s/^Version: (\s+) ( \d+ (?: \.\d+ ){3} ) $/Version:$1$version/xsm) {
        my $old_ver = $2;
        if ($old_ver ne $version) {
            say "INFO: version got changed from $old_ver to $version";
            $changes++;
            $dubbo_change = 1;

        } else {
            say "INFO: $file is already up to date: $version";
            return;
        }

    } else {
        die "$file: Version line not found";
    }

    if ($src =~ s/ngx_multi_upstream_version (\s+) ( \d+ (?: \.\d+ ){3} ) $/ngx_multi_upstream_version$1$multi_upstream_version/xsm) {
        my $old_ver = $2;
        if ($old_ver ne $multi_upstream_version) {
            say "INFO: ngx_multi_upstream version got changed from $old_ver to $version";
            $changes++;
            $multi_up_change = 1;

        } else {
            say "INFO: $file is already up to date: $version";
        }

    } else {
        die "$file: Version line not found";
    }

    if ($dubbo_change == 0 && $multi_up_change == 1) {
        die "can't change multi upstream version but does't change dubbo version";
    }

    if ($src =~ s/^Release: (\s+) (\d+) ( \%\{\?dist\} )  $/Release:${1}1$3/xsm) {
        my $old_rel = $2;
        if ($old_rel ne '1') {
            say "INFO: release number got changed from $old_rel to 1";
            $changes++;
        }

    } else {
        die "$file: Release line not found";
    }

    my $today = `date +'%a %b %e %Y'`;
    chomp $today;
    $today =~ s/\s\s+/ /g;
    #warn "today: $today";

    if ($src =~ s/^ (\%changelog\n) /${1}\* $today Yichun Zhang (agentzh) ${version}-1\n- upgraded dubbo-nginx-module-plus to $version.\n/xsm) {
        $changes++;

    } else {
        die "$file: \%changelog section not found";
    }

    if ($changes > 0) {
        open my $out, ">$file"
            or die "Cannot open $file for writing: $!\n";
        print $out $src;
        close $out;
        say "$file updated with $changes change(s).";
        system("sed 's/NGINX_VERSION/1.25.3/' dubbo-nginx-module-plus.spec | sed 's/OPENRESTY_VERSION/1.25.3.1/' > dubbo-nginx-module-plus-1.25.3.spec") == 0
                or die "Cannot generate spec for 1.25.3: $!";
    }
}

